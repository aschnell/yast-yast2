/**
 * File:
 *	Mode.ycp
 *
 * Module:
 *	Mode
 *
 * Summary:
 *	provide installation mode information
 *	Mostly value from /etc/install.inf
 *	See linuxrc documentation for detailed docs about this.
 *
 * $Id$
 *
 * Author:
 *	Klaus Kaempf <kkaempf@suse.de>
 *
 */

{
    module "Mode";

    /**
     * we're doing a fresh installation
     */
    global boolean installation = true;

    /**
     * we're doing an update
     */
    global boolean update	= false;

    /**
     * /var/adm/mount has installation data mounted
     */
    global boolean sourcemounted= false;

    /**
     * how we were booted (the type of the installation medium)
     * /etc/install.inf: Bootmode
     */
    global string boot		= "cd";

    /**
     * just testing
     */
    global boolean test		= false;

    /**
     * dry-run of dialogues
     */
    global boolean demo		= false;	// includes test_mode if set

    /**
     * dump screens to /tmp
     */
    global boolean screen_shot	= false;	// includes demo_mode if set

    /**
     * starting installation in inst-sys system
     */
    global boolean initial	= false;

    /**
     * continuing installation in target system
     */
    global boolean cont		= false;

    /**
     * hard or softboot when starting installed system ?
     */
    global boolean hardBoot	= false;

    /**
     * normal, running system
     */
    global boolean normal	= false;

    /**
     * do auto-installation
     */
    global boolean autoinst	= false;

    /**
     * configuration for auto-installation, only in running system
     */
    global boolean config	= false;

    /**
     * Preparing live-eval cd installation
     */
    global boolean live_eval	= false;

    /**
     * this is a beta-version
     */
    global boolean beta		= false;	// this is a beta version

    /**
     * no resources/packages for X11
     */
    global boolean text_fallback	= false;

    /**
     * somehow, no X11 was started
     */
    global boolean no_x11		= false;	// no x11 or not enough memory for qt

    /**
     * is set when called due to a hardware change
     */
    global boolean reprobe		= false;

    /**
     * we're running in textmode (-> UI::GetDisplayInfo())
     */
    global boolean text		= false;

    /**
     * we're running in manual mode (manual installation choosen in linuxrc)
     */
    global boolean manual		= false;

    /**
     * running via serial console
     */
    global boolean serial_console	= false;

    /**
     * Data from /etc/install.inf (linuxrc related)
     */
    global map installMap		= $[];

    /**
     * language set in install.inf
     */
    global string language		= "en_US";

    /**
     * braille mode ?
     */
    global boolean braille		= false;

    /**
     * vnc mode ?
     */
    global boolean vnc			= false;

    /**
     * install map containing data for the online update
     */
    global map youInstallMap		= $[];

    /**
     * save important data to installmap.ycp
     */
    global define void Save ()
    ``{
	installMap["updatemode"] = update;
	installMap["hardBoot"] = hardBoot;
	SCR::Write (.target.ycp, [Installation::yast2dir + "/installmap.ycp", 0600], installMap);
    }

    //---------------------------------------------------------------
    * constructor

    global define Mode () ``{

	/*
	 * test command line arguments for test mode and continue mode
	 */

	integer arg_count = size(WFM::Args());
	integer arg_no = 0;

	while ( arg_no < arg_count )
	{
	    y2debug("option #%1: %2", arg_no, WFM::Args(arg_no) );

	    if	    (WFM::Args(arg_no) == "test"	   )
	    {
		test = true;
		y2warning("***** Test mode enabled *****");
	    }
	    else if (WFM::Args(arg_no) == "initial"	    ) initial	= true;
	    else if (WFM::Args(arg_no) == "continue"	    ) cont	= true;
	    else if (WFM::Args(arg_no) == "demo"	    )
	    {
		demo = true;
		y2warning("***** Demo mode enabled *****");
	    }
	    else if (WFM::Args(arg_no) == "screenshots"  )
	    {
		screen_shot = true;
		y2warning("***** Screen shot mode enabled *****");
	    }
	    else if (WFM::Args(arg_no) == "text_fallback") text_fallback   = true;
	    else if (WFM::Args(arg_no) == "no_x11"       ) no_x11	      = true;
	    else if (WFM::Args(arg_no) == "reprobe"      ) reprobe	      = true;
	    else
	    {
		y2milestone ("skipping unknown option %1", WFM::Args(arg_no) );
	    }

	    arg_no = arg_no + 1;
	}


	if (!initial
	    && !cont
	    && !autoinst
	    && !config)
	{
	    normal = true;
	}

	// screen_shot ==> demo
	if ( screen_shot )	demo = true;

	// demo ==> test
	if ( demo )	test = true;


	// get setup data from linuxrc
	// check setup/descr/info for CD type

	if (initial)
	{
	    if ( !test )
	    {
		installMap = SCR::Read(.etc.install_inf);
	    }
	    SCR::UnmountAgent (.etc.install_inf);
	    y2milestone("Reading /etc/install.inf: initial installMap: %1", installMap);
	}
	else
	{
	    installMap = SCR::Read (.target.ycp, ["/var/lib/YaST2/installmap.ycp", $[]]);
	    y2milestone("Reading /var/lib/YaST2/installmap.ycp: installMap: %1", installMap);
	    /**
	     * restore update only during installation/update, leave
	     * it false in 'normal' mode.
	     */
	    if (!Mode::normal)
	    {
		update = installMap["updatemode"]:false;
	    }
	    hardBoot = installMap["hardBoot"]:false;
	}

	language	= installMap["language"]:"";
	live_eval	= installMap["demo"]:"0" == "1";
	text		= installMap["textmode"]:"0" == "1";
	manual		= installMap["manual"]:"" == "1";
	serial_console	= installMap["console"]:"" != "";
 	braille		= installMap["braille"]:"" != "";
	vnc		= installMap["runvnc"]:"" == "1";
	boot		= installMap["instmode"]:"cd";
	sourcemounted	= installMap["sourcemounted"]:"" == "1";
	// the value is a string
	autoinst	= installMap["autoinstall"]:"" != "";
    }
}
