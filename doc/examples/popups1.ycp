/*
 * Trivial popups example
 *
 * Author: Stefan Hundhammer <sh@suse.de>
 *
 * $Id$
 */
{
    import "Label";
    import "Popup";


    UI::OpenDialog(
		   `VBox(
			 `PushButton(`id(`msg  ),	`opt(`hstretch), "&Message Popup"	),
			 `PushButton(`id(`notify ),	`opt(`hstretch), "&Notify Popup"	),
			 `PushButton(`id(`warn ),	`opt(`hstretch), "&Warning Popup"	),
			 `PushButton(`id(`err  ),	`opt(`hstretch), "&Error Popup"		),
			 `PushButton(`id(`timed ),	`opt(`hstretch), "&Timed Message Popup"	),

			 `PushButton(`id(`yesNo ),	`opt(`hstretch), "&Yes / No Popup" ),
			 `PushButton(`id(`contCancel ), `opt(`hstretch), "C&ontinue / Cancel Popup" ),
			 `PushButton(`id(`abort ),  	`opt(`hstretch), "Confirm &Abort Popup (no changes)" ),
			 `PushButton(`id(`abort_ch ),  	`opt(`hstretch), "Confirm Abort Popup (&with changes)" ),
			 `PushButton(`id(`show_file ), 	`opt(`hstretch), "Show &File Popup" ),
			 `PushButton(`id(`show_text ), 	`opt(`hstretch), "Show Te&xt Popup" ),
			 `VSpacing(),
			 `PushButton(`id(`close), Label::CloseButton() )
			 )
		   );


    symbol button_id = `dummy;
    boolean ok = false;

    do
    {
	button_id = (symbol)UI::UserInput();

	if ( button_id == `msg )
	{
	    Popup::Message( "Hello, world!" );
	}
	else if ( button_id == `notify )
	{
	    Popup::Notify( "Notify the world!" );
	}
	else if ( button_id == `warn )
	{
	    Popup::Warning( "This is the only world we have!" );
	}
	else if ( button_id == `err )
	{
	    Popup::Error( "Cannot delete world -\nthis is the only world we have!" );
	}
	else if ( button_id == `timed )
	{
	    Popup::TimedMessage( "Just some seconds left to save the world...", 20 );
	}
	else if ( button_id == `yesNo )
	{
	    ok = Popup::YesNo( "Really delete world?" );
	}
	else if ( button_id == `contCancel )
	{
	    ok = Popup::ContinueCancel( "World will be deleted." );
	}
	else if ( button_id == `abort )
	{
	    ok = Popup::ReallyAbort( false );
	}
	else if ( button_id == `abort_ch )
	{
	    ok = Popup::ReallyAbort( true );
	}
	else if ( button_id == `show_file )
	{
	    Popup::ShowFile( "Boot Messages", "/var/log/boot.msg" );
	}
	else if ( button_id == `show_text )
	{
	    string text = (string) SCR::Read (.target.string, "/var/log/boot.msg");
	    Popup::ShowText( "Boot Messages", text );
	}
    } while ( button_id != `close );


    UI::CloseDialog();
}
