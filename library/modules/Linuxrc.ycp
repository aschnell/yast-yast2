/**
 * Module:      Linuxrc
 * File:	modules/Linuxrc.ycp
 * Purpose:	Interaction with linuxrc
 *
 * Author:	Anas Nashif <nashif@suse.de?
 * $Id$
 */
{
    module "Linuxrc";
    import "Mode";

    /**
     * Write /etc/yast.inf during installation
     * @param linuxrc	map of key value pairs for /etc/yast.inf
     * @return void
     */
    global define void WriteYaSTInf (map<string,string> linuxrc)
    {
	string yast_inf = "";
	foreach (string ykey, string yvalue, linuxrc,
	``{
	    yast_inf = yast_inf + ykey + ": " + yvalue + "\n";
	});
	y2milestone ("WriteYaSTInf(%1) = %2", linuxrc, yast_inf);

        if (!Mode::test)
        {
	    WFM::Write(.local.string, "/etc/yast.inf", yast_inf);
        }
        return;
    }

    /*
     * Copy /etc/install.inf into built system so that the
     * second phase of the installation can find it.
     * @param root mount point of system
     * @return boolean true on success
     */
    global boolean SaveInstallInf(string root)
    {
        if (Mode::initial && !Mode::test )
        {
            SCR::Execute (.target.bash, "/bin/grep -v Sourcemounted /etc/install.inf >" +
                    root + "/etc/install.inf");

            // just for debug so we can see the original install.inf later
            SCR::Execute (.target.bash, "/bin/cp /etc/install.inf " +
                    root + "/var/lib/YaST2/install.inf");
            SCR::Execute (.target.bash, "/bin/chmod 0600 " +
                    root + "/var/lib/YaST2/install.inf");
        }
        return true;
    }
}
