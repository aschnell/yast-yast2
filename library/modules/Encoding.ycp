/**
 * File:	modules/Encoding.ycp
 * Package:	yast2
 * Summary:	Provide the encoding stuff
 * Author:	Klaus Kaempf <kkaempf@suse.de>
 *
 *<BR>
 * sysconfig /etc/sysconfig/console:<BR>
 *<UL>
 *<LI>	CONSOLE_ENCODING	string	console encoding</LI>
 *</UL>
 *
 * $Id$
 */

{

module "Encoding";
textdomain "base";

import "Mode";

/**
 * Current (ISO) encoding
 */
global string console = "ISO-8859-1";
global string lang = "en_US";

/**
 * Restore data to system
 * @return console encoding
 */
global define string Restore() {

    console = (string) SCR::Read (.sysconfig.console.CONSOLE_ENCODING);
    if(console == nil) console = "";

    map m = (map) SCR::Execute( .target.bash_output, "locale -k charmap" );
    if(m == nil) m = $[];

    list<string> out = splitstring(m["stdout"]:"", "\n");
    y2milestone( "list %1", out );

    out = filter( string e, out, ``(find(e, "charmap=")==0) );
    y2milestone( "list %1", out );

    if(size(out[0]:"") > 0) {
	string enc = substring( out[0]:"", 8 );
	y2milestone( "enc %1", enc );
	enc = deletechars( enc, "\" " );
	y2milestone( "enc %1", enc );
	if(size(enc) > 0)
	    console = enc;
    }
    y2milestone( "encoding %1", console );
    return console;
}

global void SetEncLang( string new_lang )
    ``{
    lang = new_lang;
    y2milestone( "SetEncLang %1", lang );
    }

global string GetEncLang()
    ``{
    string ret = lang;
    y2milestone( "GetEncLang ret %1", ret );
    return( ret );
    }

global define string GetCodePage( string enc ) 
    ``{
    string code = "437";
    if( lang == nil)
        lang = "";

    if( enc == "iso8859-2" )
        {
        code = "852";
        }
    else if( enc == "euc-jp" || enc == "sjis" ||
	     (( enc == "utf8" ) && (substring( lang, 0, 5) == "ja_JP" )))
        {
        code = "932";
        }
    else if( enc == "gb2312" ||
	     (( enc == "utf8" ) && (substring( lang, 0, 5) == "zh_CN" )))
        {
        code = "936";
        }
    else if( enc == "big5" ||
	     (( enc == "utf8" ) && (substring( lang, 0, 5) == "zh_TW" )) ||
	     (( enc == "utf8" ) && (substring( lang, 0, 5) == "zh_HK" )))
        {
        code = "950";
        }
    else if( enc == "euc-kr" ||
	     (( enc == "utf8" ) && (substring( lang, 0, 5) == "ko_KR" )))
        {
        code = "949";
        }
    y2milestone( "GetCodePage enc %1 lang %2 ret %3", enc, lang, code );
    return code;
    }


/**
 * Constructor
 * does nothing in initial mode
 * restores console encoding from /etc/sysconfig
 * in normal mode
 */
global define void Encoding() {
    if(!Mode::initial) Restore();
    return;
}

/* EOF */
}
