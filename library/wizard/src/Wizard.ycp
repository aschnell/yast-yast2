/**
 * File:	modules/Wizard.ycp
 * Package:	yast2
 * Summary:	Wizard dialog
 * Authors:	Stefan Hundhammer <sh@suse.de>
 *
 * $Id$
 *
 * Provides the wizard dialog (common screen for all YaST2 installation
 * modules) and functions to set the contents, to replace and restore
 * special widgets.
 */

{

    module "Wizard";
    textdomain "base";

    import "Label";
    import "Popup";
    import "Directory";

    integer current_stage = 0;	// Cache for the current wizard stage (image no.)

    // keep trailing "/" !!
    string theme_path = Directory::themedir + "/current/";
    string icon_path  = theme_path;

    boolean have_fancy_ui = nil;

    /**
     * Initialization
     **/
    define void Init() ``{
	map ui_info = UI::GetDisplayInfo();

	have_fancy_ui = ui_info[ "HasImageSupport" ]:false
	    && ui_info[ "HasLocalImageSupport" ]:false
	    && ui_info[ "Depth" ]:0 >= 15
	    && ui_info[ "DefaultWidth" ]:0 >= 800
	    && ui_info[ "DefaultHeight"]:0 >= 600
	    && UI::HasSpecialWidget(`ColoredLabel);

	// have_fancy_ui = false;

	UI::SetFunctionKeys( Label::DefaultFunctionKeyMap() );
    };

    /**
     * Change the image at the top of the window (UI context)
     * @param imagedata image
     **/
    global define void SetImage( byteblock imagedata ) ``{
	if (imagedata == nil) {
	    y2warning ("SetImage(): Invalid image - using fallback" );
	    UI::ReplaceWidget( `id(`image), `Image(`opt(`zeroWidth, `easterEgg), `suseheader, "SuSE") );
	}
	else
	    UI::ReplaceWidget( `id(`image), `Image(`opt(`zeroWidth, `easterEgg), imagedata, "SuSE") );
    }


    /**
     * Returns a button box with buttons "Back", "Abort", "Next"
     * @return a widget tree
     **/
    global define term BackAbortNextButtonBox() ``{
	return `HBox(
		     `HWeight(1, `ReplacePoint(`id(`rep_back ), `PushButton(`id(`back ), `opt (`key_F8), Label::BackButton()  ) ) ),
		     `HStretch(),
		     `ReplacePoint(`id(`rep_abort), `PushButton(`id(`abort), `opt (`key_F9), Label::AbortButton() ) ),
		     `HStretch(),
		     `HWeight(1, `ReplacePoint(`id(`rep_next ), `PushButton(`id(`next), `opt (`key_F10, `default), Label::NextButton()  ) ) )
		     );
    };


    /**
     * Returns a button box with buttons "Back", "Abort Installation", "Next"
     * @return a widget tree
     **/
    global define term BackAbortInstallationNextButtonBox() ``{
	return `HBox(
		     `HWeight(1, `ReplacePoint(`id(`rep_back ), `PushButton(`id(`back ), `opt (`key_F8), Label::BackButton()  ) ) ),
		     `HStretch(),
		     `ReplacePoint(`id(`rep_abort), `PushButton(`id(`abort), `opt (`key_F9), Label::AbortInstallationButton() ) ),
		     `HStretch(),
		     `HWeight(1, `ReplacePoint(`id(`rep_next ), `PushButton(`id(`next), `opt (`key_F10, `default), Label::NextButton()  ) ) )
		     );
    };


    /**
     * Returns a button box with buttons "Back", "Next"
     * @return a widget tree
     **/
    global define term BackNextButtonBox() ``{
	return `HBox(
		     `HWeight(1, `ReplacePoint(`id(`rep_back ), `PushButton(`id(`back ), `opt (`key_F8), Label::BackButton()  ) ) ),
		     `HStretch(),
		     `HWeight(1, `ReplacePoint(`id(`rep_next ), `PushButton(`id(`next ), `opt (`key_F10, `default), Label::NextButton()  ) ) )
		     );
    };


    /**
     * Returns a button box with buttons "Cancel", "Accept"
     * @return a widget tree
     **/
    global define term CancelAcceptButtonBox() ``{
	return `HBox(
		     `HWeight(1, `Empty() ),	// Layout trick to make sure the center button is centered
		     `HStretch(),
		     `HWeight(1, `PushButton(`id(`cancel ), `opt (`key_F9), Label::CancelButton()  ) ),
		     `HStretch(),
		     `HWeight(1, `PushButton(`id(`accept ), `opt (`key_F10, `default), Label::AcceptButton()  ) )
		     );
    };


    /**
     * Returns a button box with buttons "Abort", "Accept"
     * @return a widget tree
     **/
    global define term AbortAcceptButtonBox() ``{
	return `HBox(
		     `HWeight(1, `Empty() ),	// Layout trick to make sure the center button is centered
		     `HStretch(),
		     `PushButton(`id(`abort ), `opt (`key_F9), Label::AbortButton() ),
		     `HStretch(),
		     `HWeight(1, `PushButton(`id(`accept ), `opt (`key_F10, `default), Label::AcceptButton()  ) )
		     );
    };


    /**
     * Returns a button box with buttons "Abort Installation", "Accept"
     * @return a widget tree
     **/
    global define term AbortInstallationAcceptButtonBox() ``{
	return `HBox(
		     `HWeight(1, `Empty() ),	// Layout trick to make sure the center button is centered
		     `HStretch(),
		     `PushButton(`id(`abort ), `opt (`key_F9), Label::AbortInstallationButton() ),
		     `HStretch(),
		     `HWeight(1, `PushButton(`id(`accept ), `opt (`key_F10, `default), Label::AcceptButton()  ) )
		     );
    };


    /**
     * Returns a term describing the default wizard header image.
     *
     * @param add_bottom_tile add a 3D border at the bottom?
     * @return term describing the header image.
     **/
    global define term HeaderImage( boolean add_bottom_tile ) ``{
	term image = `Image( `opt(`zeroWidth, `easterEgg), `suseheader, "SuSE");
	
	if ( have_fancy_ui == nil ) Init ();

	if ( have_fancy_ui )
	{
	    image =
		`VBox(
		      `HBox(
			    `Image( theme_path + "topRow_left.png", "YaST2" ),
			    `HWeight( 1, // Layout trick: Lower layout priority with weight
				      `HBox(
					    `Image(`opt(`tiled, `zeroWidth ), theme_path + "topRow_centerTile.png", "" ),
					    `Image(`opt(`easterEgg ) , theme_path + "topRow_right.png", "SuSE" )
					    )
				      )
			    )
		      );

	    if ( add_bottom_tile )
	    {
		image = add( image, `Image(`opt(`tiled, `zeroWidth ) , theme_path + "topRow_bottomTile.png", "" ) );
	    }
	}

	return image;
    };


    /**
     * Returns a term describing a generic wizard dialog with a configurable
     * button box.
     *
     * @param	button_box term that contains a `HBox() with buttons in it
     * @return	term describing the dialog.
     **/
    global define term GenericDialog( term button_box ) ``{
	return `VBox( `id(`WizardDialog),
		      `ReplacePoint( `id(`image ), HeaderImage( true ) ),
		      `HBox(
			    `HWeight( 30, `ReplacePoint( `id(`helpSpace), `RichText(`id(`HelpText), ""))),
			    `HSpacing(1),
			    `HWeight( 70, `VBox(
						`VSpacing(0.2),
						`HBox(
						      `ReplacePoint(`id(`rep_title_icon), `Empty() ),
						      `Heading(`id(`title), `opt (`hstretch), _("YaST2\nInitializing ...")),
						      `HStretch()
						      ),
						`VWeight( 1, // Layout trick: Lower layout priority with weight
							 `HVCenter(
								   `ReplacePoint(`id(`contents), `Empty() )
								   )
							 ),
						`ReplacePoint(`id(`rep_button_box), button_box ),
						`VSpacing(0.2)
						)
				      ),
			    `HSpacing(1)
			    )
		      );
    };



    /**
     * Simple version of easy installation steps.
     *
     * @param step_descriptions	list of installation step descriptions (strings)
     * @param current_step	number (from 1) of the current step
     * @return term describing the steps
     **/
    define term simpleEasyInstallationSteps( list<string> step_descriptions, integer current_step ) ``{

	integer step = 1;
	term vbox = `VBox();
	vbox = add( vbox, `VStretch() );

	foreach ( string step_desc, step_descriptions, ``{
	    term widget = step == current_step ? `Heading( step_desc ) : `Label( step_desc );
	    vbox = add( vbox, `HBox( `HSpacing( 3 ), `Left( widget ) ) );
	    vbox = add( vbox, `VStretch() );
	    step = step + 1;
	});

	vbox = add( vbox, `PushButton(`id(`help), `opt(`key_F1), Label::HelpButton() ) );

	return vbox;
    };


    /**
     * One of the installation steps as "cards" in "fancy" mode for easy installation.
     *
     * @param text		text to display in the card
     * @param current_step	number of the current step (from 1)
     * @param step_no		number of this step (from 1)
     * @param left_image_name	name of the image to the left  of the card
     * @param right_image_name	name of the image to the right of the card
     * @return term describing the card
     **/
    define term easyInstallationCard( string	text,
				      integer	current_step,
				      integer	step_no,
				      string	left_image_name,
				      string	right_image_name )
    ``{

	term card = nil;

	if ( step_no == current_step )
	{
	    card = `HBox(
			 `Image( theme_path + left_image_name, "" ),
			 `VBox(
			       `HBox(
				     `Image( theme_path + "active_topLeft_corner.png",  "" ),
				     `Image(`opt(`tiled, `zeroWidth ), theme_path + "active_topTile.png",  "" ),
				     `Image( theme_path + "active_insideTop_corner.png", "" )
				     ),
			       `HBox(
				     `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" ),
				     `Image( theme_path + "button_active.png", "" ),
				     `ColoredLabel(`opt(`hstretch, `vstretch),
						   text,
						   `rgb( 0, 0, 0 ),		// black foreground
						   `rgb( 238, 234, 238 ),
						   10 )				// margin
				     ),
			       `HBox(
				     `Image( theme_path + "active_bottomLeft_corner.png",  "" ),
				     `Image(`opt(`tiled, `zeroWidth ), theme_path + "active_bottomTile.png",  "" ),
				     `Image( theme_path + "active_insideBottom_corner.png", "" )
				     )
			       )
			 );
	}
	else	// not the current step
	{

	    string button_image = current_step > step_no ? "button_done.png" : "button_normal.png";

	    card = `HBox(
			 `Image( theme_path + left_image_name, "" ),
			 `VBox(
			       `HBox(
				     `Image( theme_path + "inactive_topLeft_corner.png",  "" ),
				     `Image(`opt(`tiled, `zeroWidth ), theme_path + "inactive_topTile.png",  "" ),
				     `Image( theme_path + "inactive_topRight_corner.png", "" )
				     ),
			       `HBox(
				     `Image(`opt(`tiled, `zeroHeight ), theme_path + "inactive_leftTile.png",  "" ),
				     `Image( theme_path + button_image, "" ),
				     `ColoredLabel(`opt(`hstretch, `vstretch),
						   text,
						   `rgb( 0, 0, 0 ),		// black foreground
						   `rgb( 204, 204, 204 ),
						   10 ),			// margin
				     `Image(`opt(`tiled, `zeroHeight ), theme_path + "inactive_rightTile.png",  "" )
				     ),
			       `HBox(
				     `Image( theme_path + "inactive_bottomLeft_corner.png",  "" ),
				     `Image(`opt(`tiled, `zeroWidth ), theme_path + "inactive_bottomTile.png",  "" ),
				     `Image( theme_path + "inactive_bottomRight_corner.png", "" )
				     )
			       ),
			 `Image( theme_path + right_image_name, "" ),
			 `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" )
			 );
	}

	return card;
    };


    /**
     * "Fancy" (colorful, complex) version of easy installation steps.
     *
     * @param step_descriptions	 list of installation step descriptions (strings)
     * @param current_step	 number (from 1) of the current step
     * @return term describing the steps
     **/
    define term fancyEasyInstallationSteps( list step_descriptions, integer current_step ) ``{

	return `HSquash(
			`VBox(
			      `HBox(
				    `Image( theme_path + "full_1.png", "" ),
				    `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" )
				    ),
			      easyInstallationCard( step_descriptions[0]:"Step 1",
						    current_step, 1,
						    "card1_left.png", "card1_right.png" ),
			      `HBox(
				    `Image( theme_path + "full_2.png", "" ),
				    `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" )
				    ),
			      easyInstallationCard( step_descriptions[1]:"Step 2",
						    current_step, 2,
						    "card2_left.png", "card2_right.png" ),
			      `HBox(
				    `Image( theme_path + "full_3.png", "" ),
				    `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" )
				    ),
			      easyInstallationCard( step_descriptions[2]:"Step 3",
						    current_step, 3,
						    "card3_left.png", "card3_right.png" ),
			      `VWeight( 1, // Layout trick: Lower layout priority with weight
					`HBox(
					      `VBox(
						    `Image( theme_path + "full_4.png", "" ),
						    `VWeight( 1, `Image(`opt(`tiled, `zeroHeight ), theme_path + "fullBottom_tile.png", "" ) ),
						    `Image( theme_path + "help_top.png", "" ),
						    `HBox(
							  `Top(`Image( theme_path + "help_left.png", "" ) ),
							  `PushButton(`id(`help), `opt (`key_F1), Label::HelpButton() ),
							  `Top(`Image(`opt(`zeroWidth), theme_path + "help_right.png", "" ) )
							  )
						    // `Image( theme_path + "help_bottom.png", "" )
						    ),
					      `Image(`opt(`tiled, `zeroHeight ), theme_path + "active_leftTile.png",  "" )
					      )
					)
			      )
			);
    };


    /**
     * Returns a term describing steps in an "easy installation" dialog with
     * step no. 'current_step' (from 0) highlighted.
     *
     * @param step_descriptions	list of installation step descriptions (strings)
     * @param current_step	number (from 1) of the current step
     * @return term describing the steps
     **/
    global define term EasyInstallationSteps( list<string> step_descriptions, integer current_step ) ``{
	if ( have_fancy_ui == nil ) Init ();
	return have_fancy_ui ? fancyEasyInstallationSteps( step_descriptions, current_step )
	    : simpleEasyInstallationSteps( step_descriptions, current_step );
    };


    /**
     * Set the "easy installation" step.
     *
     * @param step_descriptions	list of installation step descriptions (strings)
     * @param current_step	number (from 1) of the current step
     **/
    global define void SetEasyInstallationStep( list<string> step_descriptions, integer current_step ) ``{
	y2milestone( "Installation step #%1", current_step );
	UI::ReplaceWidget(`id(`rep_installation_steps), EasyInstallationSteps( step_descriptions, current_step ) );
    };


    /**
     * Returns a term describing an "easy installation" dialog.
     *
     * @param	button_box term that contains a `HBox() with buttons in it
     * @param	steps list of installation step descriptions (strings)
     * @return	term describing the dialog.
     **/
    global define term EasyInstallationDialog( term button_box, list<string> steps ) ``{

	if ( have_fancy_ui == nil ) Init ();

	term contents_box = `HBox(
				  `HSpacing(1),
				  `VBox(
					`HBox(
					      `ReplacePoint(`id(`rep_title_icon), `Empty() ),
					      `Heading(`id(`title), `opt (`hstretch), _("YaST2\nInitializing ...")),
					      `HStretch()
					      ),
					`VWeight(1, // Layout trick: Lower layout priority with weight
						 `HVCenter(
							   `ReplacePoint(`id(`contents), `Empty() )
							   )
						 ),
					`ReplacePoint(`id(`rep_button_box), button_box ),
					`VSpacing(0.2)
					)
				  );

	term dialog = `VBox( `id(`WizardDialog),
			     HeaderImage( ! have_fancy_ui ),
			     `VWeight(1, // Layout trick: Lower layout priority with weight
				      `HBox(
					    `ReplacePoint( `id(`rep_installation_steps), EasyInstallationSteps( steps, 1 ) ),
					    `HWeight( 1, // Layout trick: Lower layout priority with weight
						      `VBox(
							    have_fancy_ui ? `Image(`opt(`tiled, `zeroWidth ),	// Top 3D border
										   theme_path + "topRow_bottomTile.png", "" )
							    : `Empty(),
							    `VSpacing(0.2),
							    contents_box
							    )
						      )
					    )
				      )
			     );
	return dialog;
    };



    /**
     * Check if the topmost dialog is a wizard dialog
     * (i.e. has a widget with `id(`WizardDialog) )
     *
     * @return true, if topmost dialog is a wizard dialog, false otherwise
     **/
    global define boolean IsWizardDialog() ``{
	return UI::WidgetExists(`id(`WizardDialog) );
    };


    /**
     * Check if the topmost dialog has a "Next" button
     * @return true if widget with `id(`next) is present, false otherwise
     **/
    global define boolean HasNextButton() ``{
	return UI::WidgetExists(`id(`next) );
    };


    /**
     * Check if the topmost dialog has an "Accept" button
     * @return true if widget with `id(`accept) is present, false otherwise
     **/
    global define boolean HasAcceptButton() ``{
	return UI::WidgetExists(`id(`accept) );
    };


    /**
     * Open a popup dialog that displays a help text (rich text format).
     *
     * @param help_text the text to display
     **/
    global define void ShowHelp( string help_text ) ``{
	Popup::LongText(
			  // Heading for help popup window
			  _("Help"),
			  `RichText( help_text ),
			  50, 20 );
    };


    /**
     * Returns a standard wizard dialog with buttons "Next", "Back", "Abort".
     * @return term describing the dialog.
     **/
    global define term NextBackDialog() ``{
	return GenericDialog( BackAbortNextButtonBox() );
    };


    /**
     * Returns a standard wizard dialog with buttons "Cancel", "Accept"
     * @return term describing the dialog.
     **/
    global define term AcceptDialog() ``{
	return GenericDialog( CancelAcceptButtonBox() );
    };


    /**
     * Open any wizard dialog.
     * @param	dialog	a wizard dialog, e.g. Wizard::GenericDialog()
     **/
    global define void OpenDialog( term dialog ) ``{
	UI::OpenDialog( `opt(`defaultsize), dialog );
    };


    /**
     * Open a dialog with buttons "Next", "Back", "Abort"
     * and set the keyboard focus to "Next".
     **/
    global define void OpenNextBackDialog() ``{
	Wizard::OpenDialog( NextBackDialog() );
	UI::SetFocus(`id(`next) );
    };


    /**
     * Open a dialog with "Accept", "Cancel"
     * and set the keyboard focus to "Accept".
     **/
    global define void OpenAcceptDialog() ``{
	Wizard::OpenDialog( AcceptDialog() );
	UI::SetFocus(`id(`accept) );
    };


    /**
     * Close a wizard dialog.
     **/
    global define void CloseDialog() ``{
	if ( IsWizardDialog() )
	{
	    UI::CloseDialog();
	}
	else
	{
	    y2error( "Wizard::CloseDialog(): Topmost dialog is not a wizard dialog!" );
	}
    };


    /**
     * Substitute for UI::UserInput that transparently handles different
     * variations of the wizard layout. Returns `next if `next or `accept were
     * clicked, `back if `back or `cancel were clicked. Simply replace
     *    ret = UI::UserInput()
     * with
     *    ret = Wizard::UserInput()
     *
     * @return (maybe normalized) widget ID
     **/
    global define any UserInput() ``{
	any input = UI::UserInput();

	if ( input == `accept )	return `next;
	if ( input == `cancel )	return `back;

	return input;
    };


    /**
     * Create and open a typical installation wizard dialog.
     *
     * For backwards compatibility only - don't use this any more in new modules.
     **/
    global define void CreateDialog() ``{
	Wizard::OpenDialog( NextBackDialog() );
    };


    /**
     * Define how the general framework for the installation wizard should
     * look like. This function creates and shows a dialog.
     **/
    global define void SetContents(string title, term contents, string help_text,
				   boolean has_back, boolean has_next) ``{


	if ( UI::WidgetExists(`id(`next ) ) )
	{
	    UI::ChangeWidget(`id(`next), `Enabled, has_next);
	    UI::SetFocus( `id(`next) );
	}
	if ( UI::WidgetExists(`id(`back		) ) )	UI::ChangeWidget(`id(`back), `Enabled, has_back);
	if ( UI::WidgetExists(`id(`accept	) ) )	UI::SetFocus( `id(`accept ) );
	if ( UI::WidgetExists(`id(`abort	) ) )	UI::ChangeWidget(`id(`abort),`Enabled, true);
	if ( UI::WidgetExists(`id(`HelpText	) ) )	UI::ChangeWidget(`id(`HelpText), `Value, help_text);
	if ( UI::WidgetExists(`id(`title	) ) )	UI::ChangeWidget(`id(`title), `Value, title);

	UI::ReplaceWidget(`id(`contents), contents);
    };


    /**
     * Clear the wizard contents.
     *
     * This may sound silly, but it gives much faster feedback to the
     * user if used properly: Whenever the user clicks "Next" or
     * "Back", call ClearContents() prior to any lengthy
     * operation -> the user notices instant response, even though he
     * may in fact still have to wait.
     **/
    global define void ClearContents() ``{
	Wizard::SetContents("", `Empty(), "", false, false);
    };


    /**
     * Set contents of wizard dialog + its title, help_text and buttons labels. Enables both back and next button.
     * @param title title of window
     * @param contents contents of dialog
     * @param help_text help text
     * @param back_label label of back button
     * @param next_label label of next button
     **/
    global define void SetContentsButtons (string title, term contents, string help_text, string back_label, string next_label) ``{
	UI::PostponeShortcutCheck ();
	// Set button labels first to avoid geometry problems: SetContents()
	// calls ReplaceWidget() wich triggers a re-layout.

	if ( UI::WidgetExists(`id(`back ) ) )	UI::ChangeWidget(`id (`back), `Label, back_label);
	if ( UI::WidgetExists(`id(`next ) ) )	UI::ChangeWidget(`id (`next), `Label, next_label);
	Wizard::SetContents( title, contents, help_text, true, true );
	UI::CheckShortcuts ();
    };



    /**
     * Replace the wizard help subwindow with a custom widget.
     * Call this BEFORE Wizard::SetContents() to avoid geometry
     * management problems!
     *
     * Remember to restore the help subwindow with
     * RestoreHelp() before any other module is called!
     *
     * @param contents a term describing the new contents
     **/
    global define void ReplaceHelp(term contents) ``{
	UI::ReplaceWidget(`id(`helpSpace), contents);
    };


    /**
     * Restore the wizard help subwindow after Wizard::ReplaceHelp().
     **/
    global define void RestoreHelp(string help_text) ``{
	UI::ReplaceWidget(`id(`helpSpace), `RichText(`id(`HelpText), help_text ) );
    };


    /**
     * Replace the wizard 'back' button with a custom widget.
     **/
    global define void ReplaceBackButton(term contents) ``{
	UI::ReplaceWidget(`id(`rep_back), contents);
    };


    /**
     * Restore the wizard 'back' button after Wizard::ReplaceBackButton().
     **/
    global define void RestoreBackButton() ``{
	if ( UI::WidgetExists(`id(`rep_back ) ) )
	{
	    UI::ReplaceWidget(`id(`rep_back),
			      `PushButton(`id(`back), `opt (`key_F8),
					  Label::BackButton() )
			      );
	}
    };


    /**
     * Replace the wizard 'next' button with a custom widget.
     *
     * @param contents a term describing the new contents
     **/
    global define void ReplaceNextButton(term contents) ``{
	    UI::ReplaceWidget(`id(`rep_next), contents);
    };


    /**
     * Restore the wizard 'next' button after ReplaceNextButton().
     **/
    global define void RestoreNextButton() ``{
	if ( UI::WidgetExists(`id(`rep_next ) ) )
	{
	    UI::ReplaceWidget(`id(`rep_next),
			      `PushButton(`id(`next), `opt (`key_F10, `default),
					  Label::NextButton() )
			      );
	}
    };


    /**
     * Replace the wizard 'abort' button with a custom widget.
     *
     * @param contents a term describing the new contents
     **/
    global define void ReplaceAbortButton(term contents) ``{
	UI::ReplaceWidget(`id(`rep_abort), contents);
    };


    /**
     * Restore the wizard 'abort' button after ReplaceAbortButton().
     **/
    global define void RestoreAbortButton() ``{
	if ( UI::WidgetExists(`id(`rep_abort ) ) )
	{
	    UI::ReplaceWidget(`id(`rep_abort),
			      `PushButton(`id(`abort), `opt (`key_F9),
					  Label::AbortButton() )
			      );
	}
    };


    /**
     * Replace the wizard 'title' icon with a custom widget.
     *
     * @param new_icon a term describing the new contents
     **/
    global define void ReplaceTitleIcon( term new_icon ) ``{
	if ( UI::WidgetExists(`id(`rep_title_icon ) ) )
	{
	    UI::ReplaceWidget(`id(`rep_title_icon ), new_icon );
	}
    };


    /**
     * Set the wizard 'title' icon to the specified icon from the standard icon
     * directory.
     *
     * @param icon_name name (without path) of the new icon
     **/
    global define void SetTitleIcon( string icon_name ) ``{
	Wizard::ReplaceTitleIcon( `Image( icon_path + icon_name, "" ) );
    };


    /**
     * Clear the wizard 'title' icon, i.e. replace it with nothing
     **/
    global define void ClearTitleIcon() ``{
	Wizard::ReplaceTitleIcon( `Empty() );
    }

    /**
     * Replace the entire Wizard button box with a new one.
     *
     * @param button_box the new button box - something like `HBox(...)
     **/
    global define void ReplaceButtonBox( term button_box ) ``{
	UI::ReplaceWidget(`id(`rep_button_box ), button_box );
    };

    /**
     * Enable the wizard's "Abort" button
     **/
    global define void EnableAbortButton() ``{
	UI::ChangeWidget(`id(`abort), `Enabled, true );
    }


    /**
     * Disable the wizard's "Abort" button.
     * Use inside a module if there is no way to abort.
     * This button will automatically be re-enabled on the next call
     * to Wizard::SetContents().
     *
     * Note: There is no corresponding function for the wizard's "Next"
     * or "Back" buttons - this is handled in Wizard::SetContents().
     **/
    global define void DisableAbortButton() ``{
	UI::ChangeWidget(`id(`abort), `Enabled, false);
    }


    /**
     * Set the wizard installation stage, i.e. the indicator of how
     * far the installation has progressed yet.
     * Sets the wizard header image accordingly.
     * @param stage new wizard stage
     * @param force don't use the internal cache
     **/
    global define void SetStage( integer stage, boolean force ) ``{

	if ( stage == current_stage && ! force) {	// cache
	    y2debug("Stage %1 cached - doing nothing", stage );
	    return;
	}

	y2debug("Setting stage to %1", stage );

	// Set the header image according to the stage
	SetImage( (byteblock)SCR::Read (.local.byte, sformat( theme_path+"stage%1.png", stage ) ) );

	// Cache the current stage
	current_stage = stage;
    };

    /* ------------------------------------------------------------ */
    /*
     * screenshot functions
     */

    /**
     * Currently used screenshot name.
     * Initially, it must match the UI default, "yast2"
     */
    string screenshot_name = "yast2";
    /**
     * Screenshot names overriden by nested SetScreenShotName calls
     */
    list<string> screenshot_name_stack = [];

    /**
     * Declare a name for the current dialog to ease making screenshots.
     * By convention, the name is
     * {rpm-name-without-yast2}-{sorting-prefix}-{description}
     * The calls may be nested.
     * @param s eg. "mail-1-conntype"
     * @see RestoreScreenShotName
     */
    global define void SetScreenShotName (string s) ``{
	screenshot_name_stack = prepend (screenshot_name_stack, screenshot_name);
	screenshot_name = s;
	UI::SetModulename (screenshot_name);
    }

    /**
     * Restore the screenshot name.
     * If it does not match a SetScreenShotName, "yast2" is used
     * and a y2error logged.
     */
    global define void RestoreScreenShotName () ``{
	screenshot_name = screenshot_name_stack[0]:nil;
	if (screenshot_name == nil)
	{
	    screenshot_name = "yast2";
	    y2error (1, "No screenshot name to restore!");
	}
	else
	{
	    screenshot_name_stack = remove (screenshot_name_stack, 0);
	}
	UI::SetModulename (screenshot_name);
    }
    



}
