/*
 * Wizard dialog for easy installation
 *
 * Author: Stefan Hundhammer <sh@suse.de>
 *
 * $Id$
 */
{
    import "Wizard";

    global define list EasyInstallationStepDescriptions()
    ``{
	return [
		_("Language\nSelection"),
		_("Installation\nSettings"),
		_("Perform\nInstallation")
	];
    };


    term contents =
	`RadioButtonGroup(
			  `Frame( "Installation step",
				  `HVSquash(
					    `VBox(
						  `Left( `RadioButton(`id(1), `opt(`notify), "&Language Selection", true  ) ),
						  `Left( `RadioButton(`id(2), `opt(`notify), "Installation &Settings" ) ),
						  `Left( `RadioButton(`id(3), `opt(`notify), "&Perform Installation" ) )
						  )
					    )
				  )
			  );
    
    string headline  = "\"Easy Installation\" Layout";
    string help_text = "<p>Help text</p>";
    
    Wizard::OpenDialog( Wizard::EasyInstallationDialog( Wizard::AbortAcceptButtonBox(),
							EasyInstallationStepDescriptions() ) );

    Wizard::SetContents( headline, contents, help_text,
			 false,		// have back button
			 true );	// have next button

    any button = nil;

    do
    {
	button = Wizard::UserInput();

	if ( is(button, integer) )
	{
	    y2milestone( "Switching to step #%1", button );
	    Wizard::SetEasyInstallationStep( EasyInstallationStepDescriptions(), (integer)button );
	}
	    
    } while ( button != `abort && button != `next );
    
    Wizard::CloseDialog();
}
