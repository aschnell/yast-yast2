/*
 * Advanced popups example
 *
 * Author: Stefan Hundhammer <sh@suse.de>
 *
 * $Id$
 */
{
    import "Label";
    import "Popup";


    UI::OpenDialog(
		   `VBox(
			 `PushButton(`id(`yesNo ),	`opt(`hstretch), "&Yes / No popup with headline " ),
			 `PushButton(`id(`generic2 ),	`opt(`hstretch), "&Generic 2-button-popup" ),
			 `PushButton(`id(`generic3 ),	`opt(`hstretch), "&Generic 3-button-popup" ),
			 `PushButton(`id(`longText ),	`opt(`hstretch), "&Long text popup" ),
			 `VSpacing(),
			 `PushButton(`id(`close), Label::CloseButton() )
			 )
		   );


    symbol button_id = `dummy;

    do
    {
	button_id = (symbol)UI::UserInput();

	if ( button_id == `yesNo )
	{
	    Popup::YesNoHeadline( "Really delete world?",
"You in your infinite wisdom have chosen to delete this only world of ours.\n\
This will mean the end to all of mankind and all life in the only known planet\n\
known to habitate intelligent or other life in the entire universe.\n\
\n\
Even though it is well known that mankind and human character are far from perfect,\n\
we urgently request you to reconsider that decision.\n\
\n\
Are you absolutely sure you wish to delete this world?");
	}
	else if ( button_id == `generic2 )
	{
	    Popup::AnyQuestion( "Great Dilemma",
"You must now decide how to save the world.\n
\n
If make the wrong decision, creatures from outer space may decide\n
to get rid of that planet of ours to make room for some interstellar\n
hyper expressway.\n
\n
So: Do you fail to be aware to recognize the lack of not being able\n
to face this kind of indecision may kill all of mankind?",
				     "&Everything you say", "I'll &buy one of it",
				     `focus_no );
	}
	else if ( button_id == `generic3 )
	{
	    Popup::AnyQuestion3( "Greatest Dilemma of the Millennium",
"You must now decide how to save the world.\n
\n
If make the wrong decision, creatures from outer space may decide\n
to get rid of that planet of ours to make room for some interstellar\n
hyper expressway.\n
\n
So: Do you fail to be aware to recognize the lack of not being able\n
to face this kind of indecision may kill all of mankind?",
				      "&Everything you say",
				      "I'll &buy one of it",
				      "But I don't have a &car",
				      `focus_no );
	}
	else if ( button_id == `longText )
	{
	    Popup::LongText( "Bad News",
				    `RichText(
"<p>Due to unforeseen circumstances it is necessary to <b>format your hard disk</b>.</p>
<p>This may sound bad enough, but we must <b>format your brain</b>, too.</p>
<p>And once this is done, you will fail to notice that in the process
the entire planet will undergo substantial <b>rearranging of the continental shelves</b>:
The continents will all be lowered to eight feet below sea level.</p>
" ),
				    50,		// width
				    10 );	// height
	}
    } while ( button_id != `close );


    UI::CloseDialog();
}
